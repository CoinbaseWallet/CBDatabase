apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ext.kotlinVersion = "1.3.31"
    ext.rxAndroidVersion = "2.1.0"
    ext.rxKotlinVersion = "2.3.0"
    ext.okhttp3Version = "3.12.0"
    ext.moshiVersion = "1.8.0"
    ext.kotlinCoroutineVersion = "1.2.1"
    ext.ktlintVersion = "0.32.0"
    ext.dagger2Version = "2.22.1"
    ext.roomVersion = "2.1.0-alpha04"
    ext.androidXTestCore = "1.1.0"
    ext.androidXTestRunner = "1.1.0"
    ext.koptionalVersion = "1.6.0"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    ktlint "com.pinterest:ktlint:${ktlintVersion}"
    kapt(
            // Dagger 2
            "com.google.dagger:dagger-compiler:$dagger2Version",
            "com.google.dagger:dagger-android-processor:$dagger2Version",
            // Moshi
            "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion",
            // Room
            "androidx.room:room-compiler:$roomVersion"
    )

    kaptAndroidTest("androidx.room:room-compiler:$roomVersion")

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test:core:${androidXTestCore}"
    androidTestImplementation "androidx.test:runner:${androidXTestRunner}"

    // Module dependencies
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    implementation "com.gojuno.koptional:koptional:$koptionalVersion"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}